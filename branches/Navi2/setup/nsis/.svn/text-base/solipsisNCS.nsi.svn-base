; Script generated by the HM NIS Edit Script Wizard.

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "Solipsis"
!define PRODUCT_VERSION "1.1.0"
!define PRODUCT_PUBLISHER "ANR-RIAM Project"
!define PRODUCT_WEB_SITE "http://www.solipsis.org"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\Navigator.exe"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"

!define PRODUCT_PLATFORM "win32"

; MUI 1.67 compatible ------
!include "MUI.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "Solipsis.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\win-uninstall.ico"
!define MUI_WELCOMEFINISHPAGE_BITMAP "SolipsisBanner.bmp"
!define MUI_UNWELCOMEFINISHPAGE_BITMAP "SolipsisBanner.bmp"

; Welcome page
!insertmacro MUI_PAGE_WELCOME
; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!define MUI_FINISHPAGE_SHOWREADME "$INSTDIR\navigator\readmeUser.htm"
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"

; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
RequestExecutionLevel admin
OutFile "${PRODUCT_NAME}-${PRODUCT_VERSION}-${PRODUCT_PLATFORM}.exe"
InstallDir "$PROGRAMFILES\Solipsis"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show
ShowUnInstDetails show

Section -Prerequisites
  UserInfo::GetName
  ; UserInfo.dll cannot run under Win9x so we don t need to care about admin rights
  IfErrors IsAdmin
  pop $0
  UserInfo::getAccountType
  pop $0
  strCmp $0 "Admin" IsAdmin
  strCmp $0 "" IsAdmin
  MessageBox MB_OK "Administrator rights are required to install Solipsis ! (AccountType:$0)"
  Abort
  IsAdmin:

  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON1 "Solipsis requires Visual C++ 2005 Redist x86 libraries package. Do you accept VCRedistx86 installation ?" IDYES VCRedistInstall

  EndOfVCRedistInstall:
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON1 "Solipsis requires Python 2.5.2 or later. Do you accept Python installation ?" IDYES PythonInstall

  EndOfPythonInstall:
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON1 "Solipsis requires DirectX 9.0c or later. Do you accept DirectX installation ?" IDYES DXInstall

  EndOfDXInstall:
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON1 "Solipsis requires Ageia PhysX drivers 7.11.13 or later. Do you accept PhysX installation ?" IDYES PhysXInstall

  EndOfPhysXInstall:
  Goto Done

  VCRedistInstall:
  ExecWait VCRedistx86\vs2005SP1\vcredist_x86.exe
  Goto EndOfVCRedistInstall

  PythonInstall:
  ExecWait '"msiexec" /i "Python\python-2.5.2.msi"'
  Goto EndOfPythonInstall

  DXInstall:
  ExecWait DirectX\DXSETUP.exe
  Goto EndOfDXInstall

  PhysXInstall:
  ExecWait PhysX\PhysX_7.11.13_SystemSoftware.exe
  Goto EndOfPhysXInstall

  Done:
SectionEnd

Section "Navigator" SEC01
  SetOutPath "$INSTDIR\navigator"
  File /r /x resources.cfg /x Ogre.cfg /x Ogre*.log /x SOLlog.log /x *.pdb "..\..\Common\bin\navigator\Release\*.*"
  File "resources.cfg"
  CreateDirectory "$SMPROGRAMS\Solipsis"
  CreateShortCut "$SMPROGRAMS\Solipsis\Solipsis Navigator.lnk" "$INSTDIR\navigator\Navigator.exe"
  CreateShortCut "$DESKTOP\Solipsis Navigator.lnk" "$INSTDIR\navigator\Navigator.exe"
SectionEnd

Section "RakNetServer" SEC02
  SetOutPath "$INSTDIR\raknetserver"
  File /r /x stats /x RakNetServer*.log /x *.pdb /x *.ilk "..\..\Common\bin\raknetserver\Release\*.*"
  CreateShortCut "$SMPROGRAMS\Solipsis\Solipsis RakNet Server (Children Island).lnk" "$INSTDIR\raknetserver\raknetserver.exe" "-p 8660 -s 11112223 -m $\"$INSTDIR\Media\cacheServerIsland$\""
  CreateShortCut "$SMPROGRAMS\Solipsis\Solipsis RakNet Server (Delta Station).lnk" "$INSTDIR\raknetserver\raknetserver.exe" "-p 8559 -s 11112222 -m $\"$INSTDIR\Media\cacheServerDeltastation$\""
  CreateShortCut "$SMPROGRAMS\Solipsis\Solipsis RakNet Server (Rennes).lnk" "$INSTDIR\raknetserver\raknetserver.exe" "-p 8558 -s 11112235 -m $\"$INSTDIR\Media\cacheServerRennes$\""
SectionEnd

Section "WorldsServer" SEC03
  SetOutPath "$INSTDIR\WorldsServer"
  File /r /x .svn /x stats /x *.pdn /x *.bat /x setup.py /x users.xml "..\..\WorldsServer\*.*"
  CreateShortCut "$SMPROGRAMS\Solipsis\Solipsis Worlds Server.lnk" "$INSTDIR\WorldsServer\WorldsServer.py" "" "$INSTDIR\WorldsServer\WorldsServer.ico" 0
SectionEnd

Section "VoiceEngineServer" SEC04
  SetOutPath "$INSTDIR\VoiceEngineServer"
  File /r /x *.exp /x *.pdb "..\..\Common\bin\VoiceEngineServer\Release\*.*"
  CreateShortCut "$SMPROGRAMS\Solipsis\Solipsis VoiceEngine Server.lnk" "$INSTDIR\VoiceEngineServer\voiceEngineTestServer.exe"
SectionEnd

Section "Statistics" SEC05
  SetOutPath "$INSTDIR\Statistics"
  File /r /x .svn /x *.pyc /x *.csv /x *.svg "..\..\Statistics\*.*"
SectionEnd

Section "Media" SEC06
  SetOutPath "$INSTDIR\Media"
  File /r /x .svn /x NaviLocal /x lua "..\..\Media\*.*"
  CreateDirectory $INSTDIR\Media\cacheServerIsland
  CopyFiles /SILENT $INSTDIR\Media\cache\*.* $INSTDIR\Media\cacheServerIsland
  CreateDirectory $INSTDIR\Media\cacheServerDeltastation
  CopyFiles /SILENT $INSTDIR\Media\cache\*.* $INSTDIR\Media\cacheServerDeltastation
  CreateDirectory $INSTDIR\Media\cacheServerRennes
  CopyFiles /SILENT $INSTDIR\Media\cache\*.* $INSTDIR\Media\cacheServerRennes
SectionEnd

Section -AdditionalIcons
  SetOutPath $INSTDIR
  File "url.ico"
  WriteIniStr "$INSTDIR\${PRODUCT_NAME}.url" "InternetShortcut" "URL" "${PRODUCT_WEB_SITE}"
  CreateShortCut "$SMPROGRAMS\Solipsis\Visit Solipsis homepage.lnk" "$INSTDIR\${PRODUCT_NAME}.url" "" "$INSTDIR\url.ico" 0
  CreateShortCut "$SMPROGRAMS\Solipsis\Uninstall.lnk" "$INSTDIR\uninst.exe"
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\navigator\Navigator.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\navigator\Navigator.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
SectionEnd

Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) was successfully removed from your computer."
FunctionEnd

Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Are you sure you want to completely remove $(^Name) and all of its components?" IDYES +2
  Abort
FunctionEnd

Section Uninstall
  Delete "$INSTDIR\${PRODUCT_NAME}.url"
  Delete "$INSTDIR\uninst.exe"
  Delete "$INSTDIR\url.ico"

  Delete "$SMPROGRAMS\Solipsis\Uninstall.lnk"
  Delete "$SMPROGRAMS\Solipsis\Visit Solipsis homepage.lnk"
  Delete "$SMPROGRAMS\Solipsis\Solipsis VoiceEngine Server.lnk"
  Delete "$SMPROGRAMS\Solipsis\Solipsis Worlds Server.lnk"
  Delete "$SMPROGRAMS\Solipsis\Solipsis RakNet Server (Rennes).lnk"
  Delete "$SMPROGRAMS\Solipsis\Solipsis RakNet Server (Delta Station).lnk"
  Delete "$SMPROGRAMS\Solipsis\Solipsis RakNet Server (Children Island).lnk"
  Delete "$DESKTOP\Solipsis Navigator.lnk"
  Delete "$SMPROGRAMS\Solipsis\Solipsis Navigator.lnk"

  RMDir "$SMPROGRAMS\Solipsis"
  RMDir /r "$INSTDIR\Media"
  RMDir /r "$INSTDIR\Statistics"
  RMDir /r "$INSTDIR\VoiceEngineServer"
  RMDir /r "$INSTDIR\WorldsServer"
  RMDir /r "$INSTDIR\raknetserver"
  RMDir /r "$INSTDIR\navigator"
  RMDir "$INSTDIR"

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
  SetAutoClose true
SectionEnd